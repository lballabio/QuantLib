dnl
dnl $Id$
dnl
dnl This is QuantLib's configure.in
dnl Process this file with autoconf to produce a configure script.
dnl

AC_INIT(ql/qldefines.hpp)

AC_CONFIG_AUX_DIR(config)

AM_CONFIG_HEADER(ql/config.hpp)
AM_INIT_AUTOMAKE(QuantLib, 0.2.1)

dnl Check for programs

AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CXX
AC_LANG_CPLUSPLUS
AM_PROG_LIBTOOL

if test "$GXX" = yes; then
    CXXFLAGS="-g -Wall -O2"
else
    CXXFLAGS=""
fi

AC_PATH_PROG(SED, sed, notfound)
AM_CONDITIONAL(SED_FOUND, test x$SED != xnotfound)
AC_MSG_CHECKING(if C++ compiler supports namespaces (required))
AC_TRY_COMPILE(
[
namespace Foo { struct A {}; }
using namespace Foo;
],[
A a;
],[
ac_namespaces=yes
],[
config_errors=yes
ac_namespaces=no
])
AC_MSG_RESULT([$ac_namespaces])
AC_MSG_CHECKING(if C++ environment has working string class (required))
AC_TRY_LINK(
[
#include <string>
namespace std{}
using namespace std;
  class foo {
  public:
    void f(const string &s);
  };
  void foo::f(const string &s) {
    f(s.c_str());
  }
],[
   foo f1;
   f1.f("test");
],[
  ql_string_ok=yes
],[
  ql_string_ok=no
  config_error=yes
  AC_ERROR(C++ compilers string support does not work)
])
AC_MSG_RESULT([$ql_string_ok])
INCLUDES="-I`pwd`"
AC_SUBST(INCLUDES)
AC_CHECK_HEADERS(math.h cmath ctype.h cctype ctime time.h)
AC_CHECK_HEADERS(limits climits limits.h float.h)

if test $ac_cv_header_cmath = yes ; then
  ql_math=cmath
elif test $ac_cv_header_math_h = yes ; then
  ql_math=math.h
else
  AC_ERROR(Could not find cmath nor math.h)
fi
QL_CHECK_FUNCTION([sqrt],[$ql_math],[2])
QL_CHECK_FUNCTION([fabs],[$ql_math],[2])
QL_CHECK_FUNCTION([exp],[$ql_math],[2])
QL_CHECK_FUNCTION([log],[$ql_math],[2])
QL_CHECK_FUNCTION([sin],[$ql_math],[2])
QL_CHECK_FUNCTION([cos],[$ql_math],[2])
QL_CHECK_FUNCTION([pow],[$ql_math],[2,2])
QL_CHECK_FUNCTION([modf],[$ql_math],[2,&x])

if test $ac_cv_header_cctype = yes ; then
  ql_ctype=cctype
elif test $ac_cv_header_ctype_h = yes ; then
  ql_ctype=ctype.h
else
  AC_ERROR(Could not find cctype nor ctype.h)
fi
QL_CHECK_FUNCTION([toupper],[$ql_ctype],['c'])
QL_CHECK_FUNCTION([strlen],[$ql_ctype],["foo"])
QL_CHECK_FUNCTION([tolower],[$ql_ctype],['c'])

if test $ac_cv_header_ctime = yes ; then
  ql_time=ctime
elif test $ac_cv_header_time_h = yes ; then
  ql_time=time.h
else
  AC_ERROR(Could not find ctime nor time.h)
fi
QL_CHECK_FUNCTION([clock],[$ql_time],[])
QL_CHECK_FUNCTION([time],[$ql_time],[0])

QL_CHECK_FUNCTION([max],[algorithm],[1,2])
QL_CHECK_FUNCTION([min],[algorithm],[1,2])

QL_CHECK_TEMPLATE_CLASS([iterator],[std::input_iterator_tag,double],\
    [iterator],[])
QL_CHECK_TEMPLATE_CLASS([iterator_traits],[int*],[iterator],[])

QUANTLIB_LIBS="../../ql/libQuantLib.la"
QUANTLIB_CXXFLAGS=""
AC_SUBST(QUANTLIB_LIBS)
AC_SUBST(QUANTLIB_CXXFLAGS)

AC_OUTPUT(Makefile \
    ql/Makefile \
    ql/Calendars/Makefile \
    ql/CashFlows/Makefile \
    ql/DayCounters/Makefile \
    ql/FiniteDifferences/Makefile \
    ql/GeneticAlgorithms/Makefile \
    ql/Indexes/Makefile \
    ql/Instruments/Makefile \
    ql/Math/Makefile \
    ql/Minimizers/Makefile \
    ql/OneFactorModels/Makefile \
    ql/MonteCarlo/Makefile \
    ql/Patterns/Makefile \
    ql/Pricers/Makefile \
    ql/RandomNumbers/Makefile \
    ql/Solvers1D/Makefile \
    ql/TermStructures/Makefile \
    ql/Utilities/Makefile \
    Examples/Makefile \
    Examples/Caroe/Makefile \
    Examples/DiscreteHedging/Makefile \
    Examples/EuropeanOption/Makefile \
    Examples/OptimizerTest/Makefile \
    Examples/Swap/Makefile \
    quantlib-config \
    )

