# Platform-specific flags and settings
if (MSVC)
    # See cmake policy CMP00091
    # One of "MultiThreaded", "MultiThreadedDebug", "MultiThreadedDLL", "MultiThreadedDebugDLL"
    set(CMAKE_MSVC_RUNTIME_LIBRARY
        "MultiThreaded$<$<CONFIG:Debug>:Debug>$<$<BOOL:${BUILD_SHARED_LIBS}>:DLL>")

    # Export all symbols so MSVC can populate the .lib and .dll
    if (BUILD_SHARED_LIBS)
        # Temp: disable DLL builds on MSVC
        message(FATAL_ERROR
            "Shared library (DLL) builds for QuantLib on MSVC are not supported")
        set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
    endif()

    add_compile_definitions(NOMINMAX)
    add_compile_definitions(_SCL_SECURE_NO_DEPRECATE)
    add_compile_definitions(_CRT_SECURE_NO_DEPRECATE)

    # /wd4267
    # Suppress warnings: assignment of 64-bit value to 32-bit QuantLib::Integer (x64)

    # /wd26812
    # Suppress warnings: "Prefer enum class over enum" (Enum.3)

    add_compile_options(/wd4267 /wd26812)

    # warning level 3 and all warnings as errors
    add_compile_options(/W3 /WX)

    # enable parallel builds
    add_compile_options(/MP)

else()
    # warning level in line with the .github/worksflows/linux.yml compile options
    # for gcc and clang
    add_compile_options(-Wall -Wno-unknown-pragmas -Werror)
endif()
